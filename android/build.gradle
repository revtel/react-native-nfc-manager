def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

buildscript {
	repositories {
		google()
		mavenCentral()
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:7.3.1'
	}
}
apply plugin: 'com.android.library'

android {
	compileSdkVersion safeExtGet('compileSdkVersion', 33)

	def agpVersion = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION

 	if (agpVersion.tokenize('.')[0].toInteger() >= 7) {
 			namespace "community.revteltech.nfc"
   	}

	defaultConfig {
		minSdkVersion safeExtGet('minSdkVersion', 21)
    	//noinspection OldTargetApi
		targetSdkVersion safeExtGet('targetSdkVersion', 33)
	}
	lintOptions {
		abortOnError false
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
}

repositories {
	google()
	mavenCentral()
	maven {
		// All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
		url "$rootDir/../node_modules/react-native/android"
	}
}


dependencies {
	implementation 'com.facebook.react:react-native:+'
	implementation 'androidx.core:core:1.9.0'
	implementation 'androidx.appcompat:appcompat:1.6.1'
	implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'
}
